plugins {
    id "java"  // TODO: Remove this after removing all java code.
    id "scala"
    id "maven-publish"
    id "com.diffplug.gradle.spotless" version "3.27.1"
    id "org.embulk.embulk-plugins" version "0.4.1"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "pro.civitaspo"
version = "0.1.0"
description = "An filter plugin for Embulk (https://github.com/embulk/embulk/) that" +
    " copies loaded data to your defined embulk filters & output plugins."

dependencies {
    compileOnly  "org.embulk:embulk-core:0.9.23"
    compile "org.scala-lang:scala-library:2.13.1"
    // TODO: Remove this
    compile 'com.google.guava:guava:23.0'
    // The latest version of fluency is 1.3.0, but
    // This plugin use 1.1.0 for using the same msgpack version as embulk.
    // ref. https://github.com/komamitsu/fluency/compare/1.1.0...1.3.0
    compile  "org.komamitsu:fluency:1.1.0"
    compile 'com.okumin:influent-java:0.3.0'

    // TODO: Remove this
    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-deps-buffer:0.9.23"
    testImplementation "org.embulk:embulk-deps-config:0.9.23"
    testImplementation "org.embulk:embulk-core:0.9.23"
    testImplementation "org.embulk:embulk-core:0.9.23:tests"
    testImplementation "org.embulk:embulk-standards:0.9.23"
    testImplementation "org.scalatest:scalatest_2.13:3.1.1"
}

embulkPlugin {
    // mainClass = "pro.civitaspo.embulk.filter.copy.CopyFilterPlugin"
    mainClass = "org.embulk.filter.copy.CopyFilterPlugin"
    category = "filter"
    type = "copy"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

spotless {
    scala {
        scalafmt('2.4.2').configFile('.scalafmt.conf')
    }
}

gem {
    from("LICENSE.txt")
    authors = ["Civitaspo"]
    email = [ "civitaspo@gmail.com" ]
    summary = "An embulk filter plugin to copy loaded data."
    homepage = "https://github.com/civitaspo/embulk-filter-copy"
    licenses = ["MIT"]
}

gemPush {
    host = "https://rubygems.org"
}

task scalatest(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/scala/test', '-o']
    classpath = sourceSets.test.runtimeClasspath
}
